apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

// Required by bintray
// archivesBaseName is required if artifactId is different from gradle module name
// or you can add baseName to each archive task (sources, javadoc, aar)
version = '1.1.0-RC1'
group = 'com.otaliastudios'
archivesBaseName = 'bottomsheetcoordinatorlayout'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    testImplementation 'junit:junit:4.12'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'BottomSheetCoordinatorLayout'
            description 'A handy CoordinatorLayout that works well when used in a bottom sheet.'
            url 'https://github.com/natario1/BottomSheetCoordinatorLayout'

            packaging 'aar'
            groupId project.group
            artifactId 'bottomsheetcoordinatorlayout'
            version project.version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection 'https://github.com/natario1/BottomSheetCoordinatorLayout.git'
                developerConnection 'https://github.com/natario1/BottomSheetCoordinatorLayout.git'
                url 'https://github.com/natario1/BottomSheetCoordinatorLayout'

            }
            developers {
                developer {
                    id = 'natario'
                    name 'Natario'
                }
            }
        }
    }
}

def bintrayUser = System.getenv("BINTRAY_USER")
def bintrayKey = System.getenv("BINTRAY_KEY")
if (bintrayKey == null) {
    Properties props = new Properties()
    props.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = props.getProperty('bintray.user')
    bintrayKey = props.get('bintray.key')
}

bintray {
    // https://github.com/bintray/gradle-bintray-plugin
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'BottomSheetCoordinatorLayout'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/natario1/BottomSheetCoordinatorLayout.git'
        publish = true
        override = true
        version {
            name = project.version
            desc = 'BottomSheetCoordinatorLayout v. '+project.version
            released = new Date()
            vcsTag = 'v'+project.version
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile


    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// export JAVA_HOME=/Library/Java/JavaVirtualMachines/jdk1.8.0_101.jdk/Contents/Home
// ./gradlew bintrayUpload